#!/bin/zsh

function viinit() {
  SELF=${ZSH_CUSTOM_COMMON_DIR}/_init.zsh
  vi "${SELF}" && source "${SELF}"
}


alias ls='ls -bGh --color=auto'
alias gist='gist --private --shorten --open'
alias rscp='rsync -Pazv -e ssh'
alias z=wd
alias l='ls -lah'           # Long view, show hidden
alias la='ls -AF'           # Compact view, show hidden
alias ll='ls -lFh'          # Long view, no hidden

SSH_DIR=~/.ssh
SSH_CONFIG="${SSH_DIR}/config"
SSH_CONFIG_HEAD="${SSH_CONFIG}_head"
SSH_USER=eng

function visc_entry() {
    local host=$1; shift
    local hostname="${1}.lco.gtn"; shift
    echo ${hostname} > /dev/stderr

    nslookup ${hostname} &> /dev/null || return

    echo "Host ${host}" >> "${SSH_CONFIG}"
    echo "  Hostname ${hostname}" >> "${SSH_CONFIG}"
    echo "  ForwardX11 yes" >> "${SSH_CONFIG}"
    echo "  User ${SSH_USER}" >> "${SSH_CONFIG}"
    echo >> "${SSH_CONFIG}"
}

function visc() {
    vi ~/.ssh/config_head
    rm -f "${SSH_CONFIG}"

    cat "${SSH_DIR}/config_head" >> "${SSH_CONFIG}"
    
    echo >> "${SSH_CONFIG}"
    echo >> "${SSH_CONFIG}"
    echo "################################################################" >> "${SSH_CONFIG}"
    echo "# Generated by ${0:t} on $(date --iso-8601=minutes) #" >> "${SSH_CONFIG}"
    echo "################################################################" >> "${SSH_CONFIG}"
    echo >> "${SSH_CONFIG}"
    echo >> "${SSH_CONFIG}"
    echo >> "${SSH_CONFIG}"
    
    for site_dir in $(ls -d /lco/etc/tcs/???(/) | grep -vE '(sba|mfg|tst)'); do
    
        site=${site_dir:t}

        echo "site: ${site}" > /dev/stderr
    
        echo "#############" >> "${SSH_CONFIG}"
        echo "# site: ${site} #" >> "${SSH_CONFIG}"
        echo "#############" >> "${SSH_CONFIG}"
        echo >> "${SSH_CONFIG}"
    
        for box in pubsub cc core1; do
            visc_entry "${site}-${box}" "${box}.${site}"
        done
    
        for enclosure_dir in "${site_dir}"/????(/); do
            enclosure=${enclosure_dir:t}
            for telescope_dir in "${enclosure_dir}"/????(/); do
                telescope=${telescope_dir:t}
                for box in icc1 inst; do
                    visc_entry "${site}-${enclosure}-${telescope}-${box}" "${box}.${telescope}.${enclosure}.${site}"
                done
            done
        done
    
        echo >> "${SSH_CONFIG}"
        echo >> "${SSH_CONFIG}"
    
    done

    chmod 0600 "${SSH_CONFIG}"
}



function root() {
  [ ${UID} -ne 0 ] || return
  sudo su -
}

function lw {
  for f in $@; do
    ls -l $(which $f)
  done
}

alias history='history 100'
alias h='history'
function hgrep { history | grep $* }

alias grep='grep --color=auto'

alias df='df -h'
alias du='du -h -c'

alias vi='vim -lN'

alias lines='cat **/src/main/(java|groovy)/**/*.(java|groovy)(.) | wc -l'

alias vifail='vi $(=grep -sZl FAILURE **/(surefire|failsafe)-reports/*.txt)'

function extract () {
    if [ -f $1 ] ; then
        case $1 in
            *.tar.bz2)        tar xjf $1        ;;
            *.tar.gz)         tar xzf $1        ;;
            *.bz2)            bunzip2 $1        ;;
            *.rar)            unrar x $1        ;;
            *.gz)             gunzip $1         ;;
            *.tar)            tar xf $1         ;;
            *.tbz2)           tar xjf $1        ;;
            *.tgz)            tar xzf $1        ;;
            *.zip)            unzip $1          ;;
            *.Z)              uncompress $1     ;;
            *)                echo "'$1' cannot be extracted via extract()" ;;
        esac
    else
        echo "'$1' is not a valid file"
    fi
}

function mcd () {
  mkdir -p "$@" && cd "$@"
}


function sshah {
  if [[ "$1" =~ '@' ]]; then
   user=${1%%@*}
   host=${1##*@}
  else
   user=$USER
   host=$1
  fi

  if [ ! -f ~/.ssh/id_rsa.pub ]; then
    ssh-keygen -t rsa
  fi

  if [ ! -f ~/.ssh/id_rsa.pub ]; then
    die 'could not find public key'
  fi

  cat ~/.ssh/id_rsa.pub | ssh "${user}@${host}" 'mkdir -p .ssh && cd .ssh && touch authorized_keys && chmod 600 authorized_keys && cat >> authorized_keys'
}

function check-status-dir {
  for dir in $@; do
    [ -d ${dir}/.hg ] || return
    echo ${dir}:
    (cd ${dir} && cd $(hg root) && hg status && hg out)
    echo
  done
}

function check-status-basedir {
  basedir=$1; shift
  (
    cd ${basedir}
    for dir  in *(^.); do;
      check-status-dir ${dir}
    done
  )
}

function check-status {
  for basedir in $@; do
    check-status-basedir ${basedir}
  done
}
