#!/usr/bin/env zsh

DOMAIN=lco.gtn
ROOT=/lco/etc/tcs
SSH_DIR=~/.ssh
USER=eng

function entry() {
    local host=$1; shift
    local hostname="${1}.${DOMAIN}"; shift
    echo ${hostname} > /dev/stderr

    nslookup ${hostname} &> /dev/null || return

    echo "Host ${host}"
    echo "  Hostname ${hostname}"
    echo "  ForwardX11 yes"
    echo "  User ${USER}"
    echo
}

cat "${SSH_DIR}/config_head"

echo
echo
echo "################################################################"
echo "# Generated by ${0:t} on $(date --iso-8601=minutes) #"
echo "################################################################"
echo
echo
echo

for site_dir in $(ls -d ${ROOT}/???(/) | grep -v sba); do

    site=${site_dir:t}

    echo "#############"
    echo "# site: ${site} #"
    echo "#############"
    echo

    for box in pubsub cc core1; do
        entry "${site}-${box}" "${box}.${site}"
    done

    for enclosure_dir in "${site_dir}"/????(/); do
        enclosure=${enclosure_dir:t}
        for telescope_dir in "${enclosure_dir}"/????(/); do
            telescope=${telescope_dir:t}
            for box in icc1 inst; do
                entry "${site}-${enclosure}-${telescope}-${box}" "${box}.${telescope}.${enclosure}.${site}"
            done
        done
    done

    echo
    echo

done

